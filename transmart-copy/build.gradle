plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

apply plugin: 'jacoco'
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'codenarc'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.6'
    compile "com.opencsv:opencsv:${openCsvVersion}"
    compile 'commons-cli:commons-cli:1.5.0'
    compile 'org.slf4j:slf4j-api:1.7.36'
    compile 'ch.qos.logback:logback-classic:1.2.11'
    compile "org.postgresql:postgresql:${postgresqlVersion}"
    compile 'me.tongfei:progressbar:0.5.5'
    compile 'com.zaxxer:HikariCP:2.6.3'
    compile 'org.springframework:spring-jdbc:4.3.30.RELEASE'

    testCompile 'org.spockframework:spock-core:2.1-groovy-2.5'
}

def mainClass = 'org.transmartproject.copy.Copy'

run {
    mainClassName = mainClass
}

jar {
    manifest {
        attributes 'Main-Class': mainClass
        attributes 'Implementation-Version': version
    }
}

shadowJar {
    classifier = null
}

task createVersionFile {
    doLast {
        def file = new File("$projectDir/VERSION")
        file.createNewFile()
        file.text = "${version}"
    }
}
shadowJar.dependsOn 'createVersionFile'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
}

codenarc {
    configFile = new File("${project.parent.projectDir.absolutePath}/gradle/codenarc/ruleset.groovy")

    reportFormat = 'html'
}

test.testLogging.exceptionFormat = 'full'
