dependencies {
    profile "org.grails.profiles:web"

    console "org.grails:grails-console"

    provided "org.springframework.boot:spring-boot-starter-tomcat"

    runtime "com.bertramlabs.plugins:asset-pipeline-grails:$assetPipelineVersion"
    runtime 'org.postgresql:postgresql:9.3-1100-jdbc4'
    runtime 'com.oracle:ojdbc7:12.1.0.1'
    runtime 'org.javassist:javassist:3.16.1-GA'

    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.grails:grails-core"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate5"
    compile 'org.grails.plugins:spring-security-core:3.1.1'
    compile 'org.grails.plugins:spring-security-oauth2-provider:3.0.0-RC2'

    /* for SAML authentication */
    compile('org.springframework.security.extensions:spring-security-saml2-core:1.0.0.RELEASE')
    // spring security version should be in sync with that brought with
    // grails-spring-security-core
    runtime 'org.springframework.security:spring-security-config:3.2.3.RELEASE'

    //compile 'org.springframework.ldap:spring-ldap-core:2.1.0.RELEASE'
    compile 'org.springframework.security:spring-security-ldap:3.2.3.RELEASE'
    compile 'org.springframework.security:spring-security-web:3.2.3.RELEASE'
    //compile 'org.springframework.security.oauth:spring-security-oauth2:2.0.11.RELEASE'

    compile "org.swinglabs:pdf-renderer:1.0.5"
    compile 'antlr:antlr:2.7.7'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile "org.apache.lucene:lucene-core:2.4.0"
    compile "org.apache.lucene:lucene-demos:2.4.0"
    compile "org.apache.lucene:lucene-highlighter:2.4.0"
    compile "org.apache.solr:solr-solrj:6.3.0"
    compile 'commons-net:commons-net:3.3' // used for ftp transfers
    compile 'org.apache.commons:commons-math:2.2' //>2MB lib briefly used in ChartController
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.1'
    compile 'org.rosuda:Rserve:1.7.3'
    compile "com.google.guava:guava:$guavaVersion"
    compile 'log4j:apache-log4j-extras:1.2.17'
    /* for GeneGo web services: */
    compile 'axis:axis:1.4'
    compile 'jfree:jfreechart:1.0.12'
    compile 'com.thoughtworks.xstream:xstream:1.4.9'
    compile 'com.jcraft:jsch:0.1.53'
    compile 'org.fusesource.jansi:jansi:1.8'
    compile 'jline:jline:2.12.1'
    compile 'org.jfree:jfreesvg:3.1'

    compile files('lib/GenePattern.jar')

    compile project(':transmart-core-api')
    compile project(':transmart-core-db')
    compile project(':transmart-rest-api')
    compile project(':transmart-java')
    compile project(':search-domain')
    compile project(':biomart-domain')
    compile project(':transmart-legacy-db')

    testCompile project(':transmart-core-db-tests')
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails:grails-test"
    testCompile "org.grails.plugins:geb"
    testCompile "org.grails.plugins:hibernate5"
    testCompile 'org.gmock:gmock:0.9.0-r435-hyve2'
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
    testRuntime 'junit:junit:4.11'
    testRuntime 'org.hamcrest:hamcrest-core:1.3'
    testRuntime 'org.hamcrest:hamcrest-library:1.3'

    //FIXME: this block was in grails.plugins, make sure that the assets keep working
    compile project(':Rmodules')
    compile project(':transmart-gwas-plugin')
    compile project(':folder-management-plugin')
}

grails {
    exploded = true
}

assets {
    minifyJs = true
    minifyCss = true
}

task installTransmartPlugins << {
/**
 * 	Explodes the plugins in the TranSMART plugin folder.
 */
    def grailsHome = ant.project.properties."environment.GRAILS_HOME"
    def basedir = System.getProperty("base.dir")
    def pluginFiles = "${basedir}/plugins/ext-2.2.zip"
    def jsDir = "${basedir}/web-app/js/plugin"
    def gspDir = "${basedir}/grails-app/views/plugin"
    def rDir = ""

    // This is our plugin zip directory.
    def zipDir = new File("${basedir}/plugins/")

    // For each of the zips in this directory, instruct ant to unzip the file and move the contents to the directory specified by their file extension.
    zipDir.eachFile { zipFile ->

        //If we find a file.
        if (zipFile.isFile()) {
            //Unzip file to a temp directory.
            ant.unzip(src: "${zipFile}", dest: "${basedir}/plugins/temp/")

            def fileDir = new File("${basedir}/plugins/temp/")

            fileDir.eachFile { file ->
                if (file.isFile()) {
                    if (file.name.contains(".gsp")) ant.move(file: "${file}", tofile: "${gspDir}/${file.name}")
                    if (file.name.contains(".js")) ant.move(file: "${file}", tofile: "${jsDir}/${file.name}")
                    if (file.name.contains(".r")) ant.move(file: "${rDir}", tofile: "${rDir}/${file.name}")
                }
            }

            //TODO: Delete temp directory?

        }
    }

}

task executableWar(type: Copy) {
    from "${libsDir}/${project.name}-${project.version}.war"
    into "${distsDir}"
}
executableWar.dependsOn 'bootRepackage'

war {
    webInf {
        with copySpec({
            from '../Rmodules/src/main/resources/Rscripts'
            into 'Rscripts'
        })
        with copySpec({
            from 'src/main/resources/dataExportRScripts'
            into 'dataExportRScripts'
        })
        with copySpec({
            from '../open-api'
            into 'classes/public/open-api'
        })
        with copySpec({
            from 'src/main/resources/public'
            into 'classes/public'
        })
        with copySpec({
            from '../Rmodules/src/main/resources/public'
            into 'classes/public'
        })
    } //Rscripts from Rmodules into WEB-INF dir.
}

test.testLogging.exceptionFormat = 'full'

bootRun {
    jvmArgs = ['-Xmx4096m']

    // In development mode, set the TRANSMART_DEBUG_PORT environment variable to a port number to enable debugging on that port
    if(! (System.env.TRANSMART_DEBUG_PORT ?: '').empty) {
        def port = System.env.TRANSMART_DEBUG_PORT
        assert port.isInteger() && (port as int) in 0..<2**16, "TRANSMART_DEBUG_PORT environment variable is not a valid port number: '$port'"
        jvmArgs += ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=$port"]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact file("${distsDir}/${project.name}-${project.version}.war") // Publish the output of the executableWar task
        }
    }
}
publishToMavenLocal.dependsOn 'executableWar'
publish.dependsOn 'executableWar'
