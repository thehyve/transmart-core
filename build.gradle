buildscript {
    ext {
        assetPipelineVersion = '2.15.1'
        grailsVersion = '4.1.1'
        gormVersion = '7.0.2'
        guavaVersion = '30.1.1-jre'
        jansiVersion = '1.14'
        protobufVersion = '3.17.3'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.grails.org/grails/core' }
        maven { url 'https://dl.bintray.com/grails/plugins' }
        maven { url 'https://repo.thehyve.nl/content/repositories/public' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.google.guava:guava:$guavaVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion}"
        classpath "com.google.protobuf:protobuf-java:${protobufVersion}"
        classpath "com.google.protobuf:protobuf-java-util:${protobufVersion}"
        classpath 'org.grails.plugins:views-gradle:1.2.10'
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath "org.fusesource.jansi:jansi:${jansiVersion}"
        classpath 'org.owasp:dependency-check-gradle:7.1.0.1'
    }
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    analyzers.assemblyEnabled = false
    suppressionFile='suppressions.xml'
}

ext {
    transmartVersion = '17.2.15'

    assetPipelineVersion = '2.15.1'
    grailsVersion = '4.1.1'
    guavaVersion = '30.1.1-jre'
    hibernateVersion = '5.6.10.Final'
    jacksonVersion = '2.12.6'
    keycloakVersion = '18.0.0'
    liquibaseVersion = '3.10.3'
    ojdb8Version = '12.2.0.1'
    openCsvVersion = '5.4'
    postgresqlVersion = '42.2.25.jre7'
    protobufVersion = '3.17.3'
    quartzPluginVersion = '2.0.13'
    quartzVersion = '2.3.2'
    springSecurityVersion = '5.4.10'
    tomcatVersion = '9.0.63'
    logbackVersion = '1.2.7' // logback > 1.2.7 dropped a support for groovy (require logback.xml -> logback.groovy migration)
    cachePluginVersion = '5.0.1'
}

version = transmartVersion
group = 'org.transmartproject'

allprojects {
    repositories {
        repositories {
            mavenLocal()
            mavenCentral()
            maven { url 'https://repo.grails.org/grails/core' }
            maven { url 'https://repo.thehyve.nl/content/groups/public/' }
            maven { url 'https://dl.bintray.com/bluesliverx/grails-plugins' }
        }
    }
}

subprojects { project ->
    version = transmartVersion
    group = 'org.transmartproject'

    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.owasp.dependencycheck'
    dependencyCheck { analyzers.assemblyEnabled = false }

    if (project.name =~ /^(transmart-core-db-tests|transmart-core-db|transmart-rest-api|transmart-api-server)$/) {
        if (project.name =~ /^(transmart-api-server)$/) {
            apply plugin: 'war'
            apply plugin: 'org.grails.grails-web'
        } else {
            apply plugin: 'org.grails.grails-plugin'
            bootJar.enabled = false
        }
        apply plugin: 'org.grails.grails-gsp'
        apply plugin: 'asset-pipeline'

        // See https://docs.gradle.org/3.2/release-notes.html#usage-of-jansi-library-embedded-with-java-annotation-processor
        tasks.withType(JavaCompile) {
            options.fork = true
        }

        dependencyManagement {
            imports {
                mavenBom "org.grails:grails-bom:$grailsVersion"
            }
            applyMavenExclusions false
        }
    }
    if (project.name =~ /^(transmart-core-api|transmart-data|transmart-copy|transmart-api-server|transmart-schemas)$/) {
        apply plugin: 'net.linguica.maven-settings'
        apply plugin: 'maven-publish'
        publishing {
            repositories {
                maven {
                    name "nl.thehyve.nexus"
                    url "https://repo.thehyve.nl/content/repositories/releases/"
                }
            }
        }
    }
}

task prepareTestDatabase(type: Exec) {
    commandLine './scripts/prepare_database.sh'
}
prepareTestDatabase.dependsOn 'transmart-schemas:assemble'
