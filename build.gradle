buildscript {
    ext {
        assetPipelineVersion = '2.11.6'
        grailsVersion = '3.2.11'
        guavaVersion = '19.0'
        protobufVersion = '3.1.0'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.grails.org/grails/core' }
        maven { url 'https://dl.bintray.com/grails/plugins' }
        maven { url 'https://repo.transmartfoundation.org/content/repositories/public' }
        maven { url 'https://repo.thehyve.nl/content/repositories/public' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.google.guava:guava:$guavaVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath 'org.grails.plugins:hibernate5:6.0.4'
        classpath "com.google.protobuf:protobuf-java:${protobufVersion}"
        classpath "com.google.protobuf:protobuf-java-util:${protobufVersion}"
        classpath 'org.grails.plugins:views-gradle:1.0.12'
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
    }
}

apply plugin: 'idea'
apply plugin: 'maven-publish'

ext {
    transmartVersion = '17.1-SNAPSHOT'

    assetPipelineVersion = '2.11.6'
    gradleWrapperVersion = '3.5'
    grailsVersion = '3.2.11'
    guavaVersion = '19.0'
    ojdb7Version = '12.1.0.1'
    openCsvVersion = '4.0'
    postgresqlVersion = '42.1.4'
    protobufVersion = '3.1.0'
    quartzVersion = '2.0.8'
    springSecurityOauth2PluginVersion = '3.0.0-RC2'
    springSecurityPluginVersion = '3.1.1'
    springSecurityVersion = '3.2.3.RELEASE'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

version = transmartVersion
group = 'org.transmartproject'

allprojects {
    ext {
        assetPipelineVersion = assetPipelineVersion
        guavaVersion = guavaVersion
        ojdb7Version = ojdb7Version
        openCsvVersion = openCsvVersion
        postgresqlVersion = postgresqlVersion
        protobufVersion = protobufVersion
        quartzVersion = quartzVersion
        springSecurityOauth2PluginVersion = springSecurityOauth2PluginVersion
        springSecurityPluginVersion = springSecurityPluginVersion
        springSecurityVersion = springSecurityVersion
    }
    repositories {
        repositories {
            mavenLocal()
            mavenCentral()
            maven { url 'https://repo.grails.org/grails/core' }
            maven { url 'https://repo.transmartfoundation.org/content/repositories/public/' }
            maven { url 'https://repo.thehyve.nl/content/groups/public/' }
            maven { url 'https://dl.bintray.com/bluesliverx/grails-plugins' }
            //maven { url 'https://jcenter.bintray.com' }
        }
    }
}

subprojects { project ->
    version = transmartVersion
    group = 'org.transmartproject'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    if (project.name =~ /^(biomart-domain|folder-management-plugin|mock-ontology-server|Rmodules|search-domain|transmart-core-db-tests|transmart-core-db|transmart-gwas-plugin|transmart-legacy-db|transmart-rest-api|transmartApp|transmart-server|transmart-oauth|transmart-solr-indexing)$/) {
        if (project.name =~ /^(transmartApp|mock-ontology-server|transmart-server|transmart-solr-indexing)$/) {
            apply plugin: 'war'
            apply plugin: 'org.grails.grails-web'
        } else {
            apply plugin: 'org.grails.grails-plugin'
            apply plugin: 'org.grails.grails-plugin-publish'

            bootRepackage.enabled = false
        }
        apply plugin: 'org.grails.grails-gsp'
        apply plugin: 'asset-pipeline'

        dependencyManagement {
            imports {
                mavenBom "org.grails:grails-bom:$grailsVersion"
            }
            applyMavenExclusions false
        }

        if (!(project.name =~ /^(transmart-server|transmartApp|mock-ontology-server|transmart-solr-indexing)$/)) {
            grailsPublish {
                websiteUrl = 'http://transmartfoundation.org/'
                license {
                    name = 'GPL-3.0'
                }
                issueTrackerUrl = 'https://github.com/thehyve/transmart-core/issues'
                vcsUrl = 'https://github.com/thehyve/transmart-core'
                title = project.name
                desc = project.description
                developers = [johndoe: "John Doe"]
            }
        }

    }
    if (project.name =~ /^(transmart-server|transmart-core-api|transmart-data|transmart-batch)$/) {
        apply plugin: 'net.linguica.maven-settings'
        apply plugin: 'maven-publish'
        publishing {
            repositories {
                maven {
                    name "nl.thehyve.nexus"
                    url "https://repo.thehyve.nl/content/repositories/snapshots/"
                }
            }
        }
    }
}

